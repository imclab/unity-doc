! ShaderLab syntax: Blending

Blending is used to make transparent objects.

Attach:SL./PipelineBlend.png

When graphics are rendered, after all shaders have executed and all textures have been applied, the pixels are written to the screen. How they are combined with what is already there is controlled by the Blend command.

!! Syntax

:Blend Off: Turn off blending
:Blend ''SrcFactor'' ''DstFactor'': Configure & enable blending. The generated color is multiplied by the __SrcFactor__. The color already on screen is multiplied by __DstFactor__ and the two are added together.
:Blend ''SrcFactor'' ''DstFactor'', ''SrcFactorA'' ''DstFactorA'': Same as above, but use different factors for blending the alpha channel.
:BlendOp ''BlendOp'': Instead of adding blended colors together, do a different operation on them.


!! Blend operations

The following blend operations can be used:

||__Add__ ||Add source and destination together. ||
||__Sub__ ||Subtract source from destination. ||
||__RevSub__ ||Subtract destination from source. ||
||__Min__ ||Use the smaller of source and destination. ||
||__Max__ ||Use the larger of source and destination. ||
>>u43<<
||__LogicalClear__ ||Logical operation: Clear (0) __DX11.1 only__. ||
||__LogicalSet__ ||Logical operation: Set (1) __DX11.1 only__. ||
||__LogicalCopy__ ||Logical operation: Copy (s) __DX11.1 only__. ||
||__LogicalCopyInverted__ ||Logical operation: Copy inverted (!s) __DX11.1 only__. ||
||__LogicalNoop__ ||Logical operation: Noop (d) __DX11.1 only__. ||
||__LogicalInvert__ ||Logical operation: Invert (!d) __DX11.1 only__. ||
||__LogicalAnd__ ||Logical operation: And (s & d) __DX11.1 only__. ||
||__LogicalNand__ ||Logical operation: Nand !(s & d) __DX11.1 only__. ||
||__LogicalOr__ ||Logical operation: Or (s | d) __DX11.1 only__. ||
||__LogicalNor__ ||Logical operation: Nor !(s | d) __DX11.1 only__. ||
||__LogicalXor__ ||Logical operation: Xor (s ^ d) __DX11.1 only__. ||
||__LogicalEquiv__ ||Logical operation: Equivalence !(s ^ d) __DX11.1 only__. ||
||__LogicalAndReverse__ ||Logical operation: Reverse And (s & !d) __DX11.1 only__. ||
||__LogicalAndInverted__ ||Logical operation: Inverted And (!s & d) __DX11.1 only__. ||
||__LogicalOrReverse__ ||Logical operation: Reverse Or (s | !d) __DX11.1 only__. ||
||__LogicalOrInverted__ ||Logical operation: Inverted Or (!s | d) __DX11.1 only__. ||
>><<

!! Blend factors

All following properties are valid for both SrcFactor & DstFactor in the __Blend__ command. __Source__ refers to the calculated color, __Destination__ is the color already on the screen. The blend factors are ignored if __BlendOp__ is using logical operations.

||__One__ ||The value of one - use this to let either the source or the destination color come through fully. ||
||__Zero__ ||The value zero - use this to remove either the source or the destination values. ||
||__SrcColor__ ||The value of this stage is multiplied by the source color value. ||
||__SrcAlpha__ ||The value of this stage is multiplied by the source alpha value. ||
||__DstColor__ ||The value of this stage is multiplied by frame buffer source color value. ||
||__DstAlpha__ ||The value of this stage is multiplied by frame buffer source alpha value. ||
||__OneMinusSrcColor__ ||The value of this stage is multiplied by (1 - source color). ||
||__OneMinusSrcAlpha__ ||The value of this stage is multiplied by (1 - source alpha). ||
||__OneMinusDstColor__ ||The value of this stage is multiplied by (1 - destination color). ||
||__OneMinusDstAlpha__ ||The value of this stage is multiplied by (1 - destination alpha). ||


!! Details

Below are the most common blend types:

[@
Blend SrcAlpha OneMinusSrcAlpha     // Alpha blending
Blend One One                       // Additive
Blend OneMinusDstColor One          // Soft Additive
Blend DstColor Zero                 // Multiplicative
Blend DstColor SrcColor             // 2x Multiplicative
@]

!! Example
Here is a small example shader that adds a texture to whatever is on the screen already:

[@Shader "Simple Additive" {
    Properties {
        _MainTex ("Texture to blend", 2D) = "black" {}
    }
    SubShader {
        Tags { "Queue" = "Transparent" }
        Pass {
            Blend One One
            SetTexture [_MainTex] { combine texture }
        }
    }
}@]

And a more complex one, Glass. This is a two-pass shader:
# The first pass renders a lit, alpha-blended texture on to the screen. The alpha channel decides the transparency.
# The second pass renders a reflection cubemap on top of the alpha-blended window, using additive transparency.

[@Shader "Glass" {
    Properties {
        _Color ("Main Color", Color) = (1,1,1,1)
        _MainTex ("Base (RGB) Transparency (A)", 2D) = "white" {}
        _Reflections ("Base (RGB) Gloss (A)", Cube) = "skybox" { TexGen CubeReflect }
    }
    SubShader {
        Tags { "Queue" = "Transparent" }
        Pass {
            Blend SrcAlpha OneMinusSrcAlpha
            Material {
                Diffuse [_Color]
            }
            Lighting On
            SetTexture [_MainTex] {
                combine texture * primary double, texture * primary
            }
        }
        Pass {
            Blend One One
            Material {
                Diffuse [_Color]
            }
            Lighting On
            SetTexture [_Reflections] {
                combine texture
                Matrix [_Reflection]
            }
        }
    }
} @]
